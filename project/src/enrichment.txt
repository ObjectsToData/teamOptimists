// To run the macro: open this file in ImageJ; select Macros>Run Macro in imageJ top menu

run("Clear Results");

// dir = getDirectory("Choose images folder to be measured");

dir = getDirectory("startup");

countryStr = File.openAsString(dir + "temp/currentCountry.txt");
country = split(countryStr,"\n");

liststr = File.openAsString(dir + "countryData/onlyUpdatedLink" + country[0] + ".txt");
list = split(liststr,"\n");

f = File.open(dir + "countryData/" + "processedData" + country[0] + ".txt");

setBatchMode(true); 

run("Set Measurements...", "  standard median display redirect=None decimal=2");

for (i=0; i<list.length; i++) {
	path = dir+list[i];
	open(path);
	id=getImageID;

	// if image format is 24-bit RGB, measure it
	if (bitDepth==24) {

		image_ID = i + 1;
		print(image_ID + "/" + list.length + "\t" + list[i]); 

		run("HSB Stack");
		run("Convert Stack to Images");

		selectWindow("Brightness");
		//rename(list[i] + "/brightness");
		run("Measure");
		brightness_median= getResult("Median");
		close(); // close the active image - "Brightness")

		selectWindow("Saturation");
		//rename(list[i] + "/saturation");
		run("Measure");
		saturation_median = getResult("Median");
		close(); // close the active image - "Saturation")

		selectWindow("Hue");
		//rename(list[i] + "/hue");
		run("Measure");
		hue_median = getResult("Median");
		close(); // close the active image - "Hue")

		// Shapes
		open(path);

		run("Make Binary");
		
		run("Analyze Particles...", "size=10-Infinity circularity=0.00-1.00 show=Nothing include summarize");

		selectWindow("Summary"); 
	   	lines = split(getInfo(), "\n");
	   	values = split(lines[i], "\t");
	   	shape_count = values[1]; // Select count
		close();
	}

	// if image format is not 24-bit RGB, print the name of the file without saving measurements
	else {
		print("wrong format:" + "\t" + list[i]);
	}

	
	// write measurements
	print(f, "\{\"url\": \"" + list[i] + "\", \"brightness\": \"" + brightness_median + "\", \"saturation\": \"" + saturation_median + "\", \"hue\": \"" + hue_median + "\", \"shapes\": \"" + shape_count + "\"}");
}

// run("Quit");

// setBatchMode(false);

